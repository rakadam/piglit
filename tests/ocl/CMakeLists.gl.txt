include_directories(
  ${piglit_SOURCE_DIR}/tests
  ${piglit_SOURCE_DIR}/tests/util
)
link_libraries(
  piglitutilocl
  OpenCL
  
  ${OPENGL_gl_LIBRARY}
  ${OPENGL_glu_LIBRARY}
  ${GLUT_glut_LIBRARY}  
)

macro(add_ocl_file file)
ADD_CUSTOM_COMMAND(OUTPUT ${file}.c
  COMMAND oclembed ${piglit_SOURCE_DIR}/tests/ocl/${file}
  DEPENDS oclembed ${file}
)
endmacro(add_ocl_file)

macro(add_ocl_test name c_src_file ocl_src_file)
add_ocl_file(${ocl_src_file})
add_executable(ocl-${name} ${c_src_file} ${ocl_src_file}.c)
endmacro(add_ocl_test)

macro(add_ocl_compile_test name)
add_ocl_test(compile-${name} context.c ${name}.ocl)
endmacro(add_ocl_compile_test)

macro(add_ocl_dual_test name)
add_ocl_compile_test(${name})
add_ocl_test(${name} ${name}.c ${name}.ocl)
endmacro(add_ocl_dual_test)

macro(add_ocl_functestf name c_code cl_code)
ADD_CUSTOM_COMMAND(OUTPUT float-${name}.c
  COMMAND oclfunctest float-${name} float \"${c_code}\" \"${cl_code}\"
  DEPENDS oclfunctest
)
add_executable(ocl-func-float-${name} float-${name}.c)
endmacro(add_ocl_functestf)

macro(add_ocl_functestd name c_code cl_code)
ADD_CUSTOM_COMMAND(OUTPUT double-${name}.c
  COMMAND oclfunctest double-${name} double \"${c_code}\" \"${cl_code}\"
  DEPENDS oclfunctest
)
add_executable(ocl-func-double-${name} double-${name}.c)
endmacro(add_ocl_functestd)

macro(add_ocl_functestdf name c_code cl_code)
add_ocl_functestf(${name} ${c_code} ${cl_code})
add_ocl_functestd(${name} ${c_code} ${cl_code})
endmacro(add_ocl_functestdf)

add_ocl_test(context context.c dummy.ocl)
add_ocl_test(allocate-global allocate-global.c dummy.ocl)
add_ocl_test(transfer-global transfer-global.c dummy.ocl)

add_ocl_dual_test(write-global)
add_ocl_dual_test(readwrite-global)
add_ocl_dual_test(indexed-global)
add_ocl_dual_test(indexed-readwrite-global)
add_ocl_dual_test(compare)
add_ocl_dual_test(branch)
add_ocl_dual_test(float-arithm)
add_ocl_dual_test(int-arithm)
add_ocl_dual_test(loop)

add_ocl_dual_test(float-arithm2)
add_ocl_dual_test(global-id)
add_ocl_dual_test(global-size)

add_ocl_functestdf(asin "asin(x)" "asin(x)")
add_ocl_functestdf(acos "acos(x)" "acos(x)")
add_ocl_functestdf(sin "sin(x)" "sin(x)")
add_ocl_functestdf(cos "cos(x)" "cos(x)")
add_ocl_functestdf(sinh "sinh(x)" "sinh(x)")
add_ocl_functestdf(cosh "cosh(x)" "cosh(x)")
add_ocl_functestd(exp "exp(x)" "exp(x)")
add_ocl_functestf(exp "expf(x)" "exp(x)")
add_ocl_functestdf(sqrt "sqrt(x)" "sqrt(x)")
add_ocl_functestdf(log "log(x)" "log(x)")
add_ocl_functestdf(ceil "ceil(x)" "ceil(x)")
add_ocl_functestdf(floor "floor(x)" "floor(x)")
add_ocl_functestdf(log2 "log2(x)" "log2(x)")
add_ocl_functestdf(log10 "log10(x)" "log10(x)")
add_ocl_functestdf(fabs "fabs(x)" "fabs(x)")
add_ocl_functestd(pow "pow(x,3.1415)" "pow(x,3.1415)")
add_ocl_functestf(pow "powf(x,3.1415f)" "pow(x,3.1415f)")
add_ocl_functestdf(tan "tan(x)" "tan(x)")
add_ocl_functestdf(tanh "tanh(x)" "tanh(x)")
add_ocl_functestdf(trunc "trunc(x)" "trunc(x)")

add_ocl_functestf(sin-native "sin(x)" "native_sin(x)")
